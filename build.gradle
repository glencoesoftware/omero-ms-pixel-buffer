plugins {
    id 'application'
    id 'eclipse'
}

group = 'com.glencoesoftware.omero'
version = '0.6.1-SNAPSHOT'

mainClassName = 'io.vertx.core.Launcher'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases'
    }
    maven {
        url 'https://artifacts.openmicroscopy.org/artifactory/maven/'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/gs-omero-snapshots-local'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
    exclude group: 'com.drewnoakes'
    exclude group: 'com.jamonapi'
    exclude group: 'com.mortennobel'
    exclude group: 'com.zeroc', module: 'icestorm'
    exclude group: 'commons-beanutils'
    exclude group: 'commons-codec'
    exclude group: 'geronimo-spec'
    exclude group: 'gnu.getopt'
    exclude group: 'net.sf.ehcache'
    exclude group: 'org.apache.commons'
    exclude group: 'org.apache.httpcomponents'
    exclude group: 'org.apache.pdfbox'
    exclude group: 'org.apache.xmlgraphics'
    exclude group: 'org.ini4j'
    exclude group: 'org.javassist'
    exclude group: 'org.quartz-scheduler'
    exclude group: 'org.subethamail'
    exclude group: 'xerces'
    exclude group: 'xalan'
}

dependencies {
	implementation 'io.prometheus:simpleclient_vertx:0.6.0'
    implementation 'io.zipkin.brave:brave:5.6.8'
    implementation 'io.zipkin.brave:brave-http:4.13.6'
    implementation 'io.zipkin.brave:brave-instrumentation-http:5.6.8'
    implementation 'io.zipkin.reporter2:zipkin-sender-okhttp3:2.10.0'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'org.slf4j:log4j-over-slf4j:1.7.32'
    implementation 'com.glencoesoftware.omero:omero-ms-core:0.6.1'
    implementation 'io.vertx:vertx-web:3.8.1'
    implementation 'io.vertx:vertx-config:3.8.1'
    implementation 'io.vertx:vertx-config-yaml:3.8.1'
    implementation 'org.openmicroscopy:omero-blitz:5.5.12'
    implementation 'io.prometheus.jmx:collector:0.12.0'
    implementation 'io.prometheus:simpleclient_hotspot:0.8.0'
    implementation 'com.zeroc:icegrid:3.6.4'
    testImplementation 'org.testng:testng:6.10'
    testImplementation 'org.mockito:mockito-core:2.+'
}

jar {
    manifest {
        attributes(
            "Main-Class": mainClassName,
            "Main-Verticle": "com.glencoesoftware.omero.ms.pixelbuffer.PixelBufferMicroserviceVerticle",
            "Implementation-Version": archiveVersion
        )
    }
}

test {
  // enable TestNG support (default is JUnit)
  useTestNG()
}

distributions {
    main {
        contents {
            from("$projectDir") {
                include 'README.md'
            }
        }
    }
}

applicationDefaultJvmArgs = [
    "-Dvertx.logger-delegate-factory-class-name=io.vertx.core.logging.SLF4JLogDelegateFactory"
]
